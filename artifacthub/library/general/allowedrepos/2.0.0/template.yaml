apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: k8sallowedreposv2
  annotations:
    metadata.gatekeeper.sh/title: "Allowed Repositories"
    metadata.gatekeeper.sh/version: 2.0.0
    description: >-
      Require container images to begin with a string from a specified list of repositories or registries,
      or match specific image names. 
      The rule has been improved to allow users to distinguish between repository names, registry names, and image names,
      preventing bypass methods that existed in the past.
      For example, the rule now appends a trailing slash to repository or registry names if one is not already present.
      This enhancement is crucial for security, as it helps prevent attackers from bypassing controls by creating subdomains or
      malicious repositories that begin with the same name as those declared in the constraint file. Without the slash,
      a defined registry like fictional.registry.example could be exploited by an attacker setting up a malicious registry at
      fictional.registry.example.malicious.com. Similarly, defining a repository as "myrepo" might allow an attacker to bypass
      restrictions by creating a repository named "myrepoevil" on DockerHub.
      Additionally, this policy checks Docker image names to ensure they either match the names specified in the constraint file,
      end with a colon (:) for <image-name>:<tag> format, or with an at symbol (@) for <image-name>@<digest> format.
      This prevents attackers from using variations of valid image names to circumvent security controls.
spec:
  crd:
    spec:
      names:
        kind: K8sAllowedReposV2
      validation:
        # Schema for the `parameters` field
        openAPIV3Schema:
          type: object
          properties:
            repos:
              description: The allowed prefixes for repositories or registries of a container image.
              type: array
              items:
                type: string
            images:
              description: The allowed prefixes for the names of container images.
              type: array
              items:
                type: string
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package k8sallowedreposv2

        # This policy checks Docker image names to ensure they either match the names specified in the constraint file
        # or end with a colon (:) for <image-name>:<tag> format
        # or with an at symbol (@) for <image-name>@<digest> format
        check_image_suffix(image_name, suffix){
          count(image_name) ==  count(suffix)
        }

        check_image_suffix(image_name,suffix){
          substring(image_name, count(suffix), 1) == ":"
        }

        check_image_suffix(image_name,suffix){
          substring(image_name, count(suffix), 1) == "@"
        }

        # This function appends a trailing slash to repository or registry names if one is not already present.
        # this enhancement is crucial for security, as it helps prevent attackers from bypassing controls by
        # creating subdomains or malicious repositories that begin with the same name as those declared in the constraint file.
        # For example, without the slash, a defined registry like fictional.registry.example could be exploited by an attacker
        # setting up a malicious registry at fictional.registry.example.malicious.com.
        # Similarly, defining a repository as "myrepo" might allow an attacker to bypass restrictions by creating a repository named "myrepoevil" on DockerHub.
        ensure_trailing_slash(repo) = result {
            not endswith(repo,"/")
            result := concat("", [repo, "/"])
        } else = repo {
            endswith(repo,"/")
        }

        # Define array of all repositories or registries with a trailing slash.
        processed_repos := [ensure_trailing_slash(repo) | repo := input.parameters.repos[_]]
        processed_images := [image | image := input.parameters.images[_]]

        # Create array for images with and without a slash.
        imagesWithSlash := [image | image := input.parameters.images[_]; contains(image, "/")]
        imagesWithoutSlash := [image | image := input.parameters.images[_]; not contains(image, "/")]

        # Concatenate user-defined repositories and registries with images to define permitted sources
        permitted_sources = array.concat(processed_repos, processed_images)

        # Check whether the given user input is a valid Docker image.
        check_image_policy(container_image)
        {
            not contains(container_image, "/")
            matching_prefixes := {prefix |
            prefix := imagesWithoutSlash[_]  # Iterate over each prefix in the list
            startswith(container_image, prefix)  # Check if the container_image starts with this prefix
            check_image_suffix(container_image,prefix)
            }

            not count(matching_prefixes) == 0
        }

        check_image_policy(container_image)
        {
            matching_prefixes := {prefix |
            prefix := imagesWithSlash[_]  # Iterate over each prefix in the list
            startswith(container_image, prefix)  # Check if the container_image starts with this prefix
            check_image_suffix(container_image,prefix)
            }
          
            not count(matching_prefixes) == 0
        }

        violation[{"msg": msg}] {
          container := input.review.object.spec.containers[_]
          not strings.any_prefix_match(container.image, processed_repos)
          not check_image_policy(container.image)
          msg := sprintf("container <%v> has an invalid image source <%v>, allowed sources are <%v>", [container.name, container.image, permitted_sources])
        }

        violation[{"msg": msg}] {
          container := input.review.object.spec.initContainers[_]
          not strings.any_prefix_match(container.image, processed_repos)
          not check_image_policy(container.image)
          msg := sprintf("initContainer <%v> has an invalid image source <%v>, allowed sources are <%v> ", [container.name, container.image, permitted_sources])
        }

        violation[{"msg": msg}] {
          container := input.review.object.spec.ephemeralContainers[_]
          not strings.any_prefix_match(container.image, processed_repos)
          not check_image_policy(container.image)
          msg := sprintf("ephemeralContainer <%v> has an invalid source repo <%v>, allowed sources are <%v>", [container.name, container.image, permitted_sources])
        }
